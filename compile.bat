@echo off
rem ============================================================
rem  Monolith Finder - compile source and package into a jar
rem  Output: monolith-finder.jar (root)
rem ============================================================
setlocal
cd /d "%~dp0"

rem clean previous build
if exist build rmdir /s /q build
mkdir build

rem extract native libraries on first build
if not exist natives (
  echo Extracting native libraries to natives\ ...
  mkdir natives
  tar -xf lib\windows_natives.jar -C natives
)

echo Compiling...
dir /s /b src\*.java > sources.txt
javac -encoding UTF-8 -d build -cp "lib\lwjgl.jar;lib\lwjgl_util.jar;lib\jinput.jar" @sources.txt
if errorlevel 1 (
  del sources.txt
  goto :eof
)
del sources.txt

echo Merging library classes into build...
pushd build
jar xf ..\lib\lwjgl.jar
jar xf ..\lib\lwjgl_util.jar
jar xf ..\lib\jinput.jar
popd

echo Packaging monolith-finder.jar ...
jar cfe monolith-finder.jar monolith.app.MonolithMapApp -C build .

echo Done. Run with run.bat [seed]
endlocal